apply plugin: "scala"
apply plugin: "idea"

repositories {
  mavenCentral()
  maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

ext.useKepler = true
ext.keplerDir = file("kepler")

dependencies {
    if (useKepler) {
      scalaTools fileTree(dir: "kepler/dists/latest/lib")
      compile files("kepler/dists/latest/lib/scala-compiler.jar")
      compile files("kepler/dists/latest/lib/scala-library.jar")
    } else {
      scalaTools "org.scala-lang:scala-compiler:2.10.0-SNAPSHOT"
      compile "org.scala-lang:scala-compiler:2.10.0-SNAPSHOT"
      compile "org.scala-lang:scala-library:2.10.0-SNAPSHOT"
    }

    testCompile "junit:junit:4.10"
}

compileScala {
  scalaCompileOptions.with {
    additionalParameters = ["-Xmacros"]
    if (useKepler) {
      additionalParameters += ["-Ymacro-debug", "-Ymacro-copypaste"]
    }
  }
}

compileTestScala {
  scalaCompileOptions.with {
    //loggingLevel = "debug"
    //loggingPhases = ["namer", "typer", "pickler", "explicitouter", "lambdalift"]
    additionalParameters = ["-Yrangepos"]
  }
}

idea {
    project {
        jdkName = "1.6"
    }
}

test {
  testLogging.showStandardStreams = true
}

if (useKepler) {
  task buildKepler {
    inputs.property "kepler version", {
      def ps = "git describe".execute(null, keplerDir)
      ps.inputStream.text
    }
    outputs.dir "$keplerDir/dists/latest/lib"
    doLast {
      //ant.ant(dir: keplerDir, antfile: "build.xml", target: "fastdist", inheritAll: false)
      exec {
        environment "ANT_OPTS", "-Xms512M -Xmx2048M -Xss1M -XX:MaxPermSize=128M"
        workingDir = keplerDir
        executable = "ant"
        args "fastdist"
      }
    }
  }
  compileScala.dependsOn(buildKepler)
}

task wrapper(type: Wrapper) {
    gradleVersion = "1.0-milestone-9"
}

